#version 450
#extension GL_GOOGLE_include_directive : enable

#include "glsl/octree.glsl"
#include "glsl/camera.glsl"
#include "glsl/cursor.glsl"
#include "glsl/mouse.glsl"
#include "glsl/ray.glsl"

#define STACK_SIZE 23
#define EPS 1.1920929e-7 // 2^(-23)

layout (binding = 0) buffer WorldBuffer {
    Octree octree;
};

layout (binding = 1) buffer CursorBuffer {
    Cursor cursor;
};

layout(push_constant) uniform PushConstants {
    Camera camera;
    Mouse mouse;
};

layout (local_size_x = 1) in;

struct StackItem {
	uint node;
	float t_max;
} stack[STACK_SIZE];

void octree_raymarch_coarse_cursor(vec3 o, vec3 d) {
    // Octree resides at coordinates [8, 16] ([1, 2] * 8)
    float lower_bound = 8.0;
    float upper_bound = lower_bound * 2.0;
    float mid_point = (lower_bound + upper_bound) * 0.5;
    float mirror = lower_bound + upper_bound;

    // Get rid of small ray direction components to avoid division by zero
    d.x = abs(d.x) > EPS ? d.x : (d.x >= 0 ? EPS : -EPS);
	d.y = abs(d.y) > EPS ? d.y : (d.y >= 0 ? EPS : -EPS);
	d.z = abs(d.z) > EPS ? d.z : (d.z >= 0 ? EPS : -EPS);

    // Precompute the coefficients of tx(x), ty(y), and tz(z)
    vec3 t_coef = 1.0 / -abs(d);
	vec3 t_bias = t_coef * o;

    // Select octant mask to mirror the coordinate system so
    // that ray direction is negative along each axis.
    uint oct_mask = 0;

    if (d.x > 0.0)
		oct_mask ^= 1, t_bias.x = mirror * t_coef.x - t_bias.x;
	if (d.y > 0.0)
		oct_mask ^= 2, t_bias.y = mirror * t_coef.y - t_bias.y;
	if (d.z > 0.0)
		oct_mask ^= 4, t_bias.z = mirror * t_coef.z - t_bias.z;
    
    // Initialize the active span of t-values.
	float t_min = max(max(upper_bound * t_coef.x - t_bias.x, upper_bound * t_coef.y - t_bias.y), upper_bound * t_coef.z - t_bias.z);
	float t_max = min(min(lower_bound * t_coef.x - t_bias.x, lower_bound * t_coef.y - t_bias.y), lower_bound * t_coef.z - t_bias.z);
    // Negative t_min means we start inside the root I think
    t_min = max(t_min, 0.0);
    float h = t_max;

    // If the ray missed the root
    if (t_min > t_max) {
        // Side at index 3 doesn't exist so the cursor won't be shown
        cursor.highlighted_side = 3; 
        return;
    }  

    // Root is located at the last location in WorldBuffer
    uint parent = octree.descriptors[MEMORY_SIZE - 1];
    // Cached child descriptor
    uint descriptor = 0;
    vec3 pos = vec3(lower_bound);
    uint idx = 0;

    if (mid_point * t_coef.x - t_bias.x > t_min)
		idx ^= 1, pos.x = mid_point;
	if (mid_point * t_coef.y - t_bias.y > t_min)
		idx ^= 2, pos.y = mid_point;
	if (mid_point * t_coef.z - t_bias.z > t_min)
		idx ^= 4, pos.z = mid_point;

    uint scale = STACK_SIZE - 1;
    float scale_exp2 = 0.5 * lower_bound;

    while (scale < STACK_SIZE) {
        if (descriptor == 0)
            descriptor = octree.descriptors[((parent >> 8) + (idx ^ oct_mask))];

        // Determine maximum t-value of the cube by evaluating tx(), ty(), and tz() at its corner
        vec3 t_corner = pos * t_coef - t_bias;
        float tc_max = min(min(t_corner.x, t_corner.y), t_corner.z);

        if ((parent & 1 << (idx ^ oct_mask)) != 0 && t_min <= t_max) {
            // INTERSECT
            float tv_max = min(t_max, tc_max);
            float half_scale_exp2 = scale_exp2 * 0.5;
            // Intersect active t-span with the cube and evaluate tx(), ty(), and tz() at the center of the voxel
            vec3 t_center = half_scale_exp2 * t_coef + t_corner;

            if (t_min <= tv_max) {
                // If ray has hit a leaf child
                if (scale == 20) {
                    // Undo the mirroring of the coordinate system
                    if ((oct_mask & 1) != 0)
		                pos.x = ((upper_bound - 1.0) - pos.x) + lower_bound;
	                if ((oct_mask & 2) != 0)
	                	pos.y = ((upper_bound - 1.0) - pos.y) + lower_bound;
	                if ((oct_mask & 4) != 0)
	                	pos.z = ((upper_bound - 1.0) - pos.z) + lower_bound;

                    cursor.pos = pos;

                    // Determine which side is hit
                    vec3 t_opp_corner = scale_exp2 * t_coef + t_corner;

                    if (t_opp_corner.x > t_opp_corner.y && t_opp_corner.x > t_opp_corner.z) {
                        cursor.highlighted_side = 0; 
                    }
                    else if (t_opp_corner.y > t_opp_corner.z) {
                        cursor.highlighted_side = 1; 
                    }
                    else {
                        cursor.highlighted_side = 2; 
                    }

                    return;
                }

                // PUSH
                if (tc_max < h)
                    stack[scale] = StackItem(parent, t_max);

                h = tc_max;
                parent = descriptor;

                // Select child voxel that the ray enters first
                idx = 0;
                scale -= 1;
                scale_exp2 = half_scale_exp2;

                if (t_center.x > t_min)
    				idx ^= 1, pos.x += scale_exp2;
    			if (t_center.y > t_min)
    				idx ^= 2, pos.y += scale_exp2;
    			if (t_center.z > t_min)
    				idx ^= 4, pos.z += scale_exp2;

                descriptor = 0;
                t_max = tv_max;

                continue;
            }
        }

        // ADVANCE
        uint step_mask = 0;

		if (t_corner.x <= tc_max)
			step_mask ^= 1, pos.x -= scale_exp2;
		if (t_corner.y <= tc_max)
			step_mask ^= 2, pos.y -= scale_exp2;
		if (t_corner.z <= tc_max)
			step_mask ^= 4, pos.z -= scale_exp2;

        // Update active t-span and flip bits of the child slot index
        t_min = tc_max;
        idx ^= step_mask;

        // If the ray exists the octree without hitting a leaf voxel
        if (pos.x < 8.0 || pos.y < 8.0 || pos.z < 8.0) {
            // Side at index 3 doesn't exist so the cursor won't be shown
            cursor.highlighted_side = 3; 
            return;
        }

        // Proceed with pop if the bit flips disagree with the ray direction
        if ((idx & step_mask) != 0) {
            // POP
			// Find the highest differing bit between two positions
            uint differing_bits = 0;

			if ((step_mask & 1) != 0)
				differing_bits |= floatBitsToUint(pos.x) ^ floatBitsToUint(pos.x + scale_exp2);
			if ((step_mask & 2) != 0)
				differing_bits |= floatBitsToUint(pos.y) ^ floatBitsToUint(pos.y + scale_exp2);
			if ((step_mask & 4) != 0)
				differing_bits |= floatBitsToUint(pos.z) ^ floatBitsToUint(pos.z + scale_exp2);

            // Find MSB
            scale = findMSB(differing_bits);
            scale_exp2 = uintBitsToFloat((scale - STACK_SIZE + 127) << 23) * lower_bound; // exp2f(scale - s_max) * lower_bound

            // Restore parent voxel from the stack
            parent = stack[scale].node;
			t_max = stack[scale].t_max;

            // Round cube position and extract child slot index
            uint shx = floatBitsToUint(pos.x) >> scale;
			uint shy = floatBitsToUint(pos.y) >> scale;
			uint shz = floatBitsToUint(pos.z) >> scale;
			pos.x = uintBitsToFloat(shx << scale);
			pos.y = uintBitsToFloat(shy << scale);
			pos.z = uintBitsToFloat(shz << scale);

            idx = (shx & 1) | ((shy & 1) << 1) | ((shz & 1) << 2);

            // Prevent same parent from being stored again and invalidate cached child descriptor
            h = 0.0;
			descriptor = 0;
        } 
    }
}

void main() {
    Ray ray = create_ray(camera, mouse.coordinate.x, mouse.coordinate.y);

    octree_raymarch_coarse_cursor(ray.origin, ray.direction);
}